plugins {
    id 'java-library'
    id "idea"
    id "com.adarshr.test-logger" version "2.1.1"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "maven-publish"
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id "com.jfrog.bintray" version "1.8.5"
    id "com.github.ben-manes.versions" version "0.36.0"
    id 'net.researchgate.release' version '2.8.1'
}

def isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
def publishVersion = isBuildSnapshot ? project.version.replaceAll(/-SNAPSHOT$/, "") : project.version;

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kestra/maven" + (isBuildSnapshot ? "-snapshot" : "") }
}

sourceCompatibility = 11
group "org.kestra.task.templates"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

dependencies {
    // lombok
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"

    // micronaut
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    compileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
    compileOnly "io.micronaut:micronaut-inject"
    compileOnly "io.micronaut:micronaut-validation"

    // kestra
    compileOnly group: "org.kestra", name: "core", version: kestraVersion

    // libs included in the final jar
    api group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
}


/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
test {
    useJUnitPlatform()
}

testlogger {
    theme "mocha-parallel"
    showExceptions true
    showFullStackTraces true
    showStandardStreams true
    showPassedStandardStreams false
    showSkippedStandardStreams true
}

dependencies {
    testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "io.micronaut.test:micronaut-test-junit5"

    // test deps needed only for to have a runner
    testImplementation group: "org.kestra", name: "core", version: kestraVersion
    testImplementation group: "org.kestra", name: "repository-memory", version: kestraVersion
    testImplementation group: "org.kestra", name: "runner-memory", version: kestraVersion
    testImplementation group: "org.kestra", name: "storage-local", version: kestraVersion

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
}

/**********************************************************************************************************************\
 * Publish
 **********************************************************************************************************************/
jar {
    manifest {
        attributes(
                "X-Kestra-Title": project.name,
                "X-Kestra-Group": project.group,
                "X-Kestra-Version": project.version
        )
    }
}

shadowJar {
    archiveClassifier.set(null)
    mergeServiceFiles()
}

github {
    user 'kestra-io'
    license 'Apache'
}

publishing {
    publications {
        BintrayMavenPublication(MavenPublication) {
            version publishVersion

            groupId project.group
            artifactId project.name

            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                configurations.api.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['BintrayMavenPublication']
    publish = true
    override = isBuildSnapshot
    pkg {
        userOrg = 'kestra'
        name = project.name
        repo = isBuildSnapshot ? 'maven-snapshot' : 'maven'
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        version {
            name = publishVersion
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ")
            vcsTag = "v" + publishVersion
        }
    }
}

/**********************************************************************************************************************\
 * Version
 **********************************************************************************************************************/
release {
    preCommitText = 'chore(version):'
    preTagCommitMessage = 'update to version'
    tagCommitMessage = 'tag version'
    newVersionCommitMessage = 'update snapshot version'
    tagTemplate = 'v${version}'
    buildTasks = ['classes']
}

/**********************************************************************************************************************\
 * Dev
 **********************************************************************************************************************/
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
